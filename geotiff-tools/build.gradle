import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
  id "groovy"
  id "java-library"
  id "maven-publish"
  id "signing"

  id "org.checkerframework" version "0.6.3"
}

group = "de.hendrikjanssen"
archivesBaseName = "geotiff-tools"
version = "0.2.0"

repositories {
  mavenCentral()
}

dependencies {
  api("org.geolatte:geolatte-geom:1.8.2")

  implementation("com.twelvemonkeys.imageio:imageio-tiff:3.8.2")

  // ------------ Test ------------

  testImplementation(platform("org.junit:junit-bom:5.9.0"))
  testImplementation("org.junit.jupiter:junit-jupiter")

  testImplementation("org.spockframework:spock-core:2.1-groovy-3.0")

  testImplementation(platform("org.codehaus.groovy:groovy-bom:3.0.12"))
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

task javadocJar(type: Jar) {
  archiveClassifier.set("javadoc")
  from javadoc
}

task sourcesJar(type: Jar) {
  archiveClassifier.set("sources")
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = group
      artifactId = project.name
      version = version

      from components.java
    }
  }
}

test {
  useJUnitPlatform()

  testLogging {
    showExceptions = true
    showStandardStreams = true
    events = [TestLogEvent.SKIPPED, TestLogEvent.FAILED, TestLogEvent.PASSED]
    exceptionFormat = TestExceptionFormat.FULL
  }
}